// Copyright 2022 Citra Emulator Project
// Licensed under GPLv2 or any later version
// Refer to the license.txt file included.

#version 450 core
#extension GL_EXT_samplerless_texture_functions : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
layout(binding = 0) uniform highp texture2D depth;
layout(binding = 1) uniform lowp utexture2D stencil;
layout(binding = 2, r32ui) uniform highp writeonly uimage2D color;

layout(push_constant, std140) uniform ComputeInfo {
    mediump ivec2 src_offset;
};

void main() {
    ivec2 dst_coord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 tex_coord = src_offset + dst_coord;
    highp uint depth_val = uint(texelFetch(depth, tex_coord, 0).x * (exp2(24.0) - 1.0));
    lowp uint stencil_val = texelFetch(stencil, tex_coord, 0).x;
    highp uint value = stencil_val | (depth_val << 8);
    imageStore(color, dst_coord, uvec4(value));
}
